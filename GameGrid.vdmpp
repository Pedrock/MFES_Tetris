class GameGrid
/*
 A classe GameGrid representa a grelha do jogo, que é representada a partir de uma matriz
 de diferentes tipos possiveis de células.
*/

types

-- Possiveis tipos de células
public CellType = <Blank> | <Cyan> | <Yellow> | <Purple> | <Green> | <Red> | <Blue> | <Orange>;

-- Grelha do jogo, que é constituida por uma matriz de células
public Grid = seq of seq of CellType;


values

-- Altura da grelha
public HEIGHT : nat1 = 22;

-- Largura da grelha
public WIDTH : nat1 = 10;


instance variables

-- Grelha inicial encontra-se vazia
public grid : Grid := buildEmptyGrid(WIDTH, HEIGHT);


operations

-- Retorna a célula que se encontra na posição x,y
public getCell : nat1*nat1 ==> CellType
getCell(x, y) == return grid(y)(x)
pre x <= WIDTH and y <= HEIGHT
post RESULT = grid(y)(x);

public addTetramino : Tetramino ==> ()
addTetramino(tetramino) == (
	for all tetraminoY in set inds tetramino.getCurrentMatrix() do (
		let line = tetramino.getCurrentMatrix()(tetraminoY) in (
			for all tetraminoX in set inds line do (
				let cellX = tetramino.x - 1 + tetraminoX, cellY = tetramino.y + 1 - tetraminoY in (
				if (line(tetraminoX) <> <Blank>) then
						grid(cellY)(cellX) := line(tetraminoX)
				)
			)
		)
	)
);

public removeLine : nat1 ==> ()
removeLine(y) == (
	for i = y to HEIGHT - 1 do (
		grid(i) := grid(i + 1);
	);
	grid(HEIGHT) := buildEmptyRow(WIDTH);
);

functions

-- Criação de uma linha da grelha vazia
private buildEmptyRow : nat -> seq of CellType
	buildEmptyRow(length) == 
		[<Blank> | v in set {1,...,length} & v > 0]; -- v > 0 utilizado para remover warning

-- Criação de uma grelha vazia
private buildEmptyGrid : nat1*nat1 -> Grid
	buildEmptyGrid(width, height) == 
		[buildEmptyRow(width) | v in set {1,...,height} & v > 0 ]; -- v > 0 utilizado para remover warning


end GameGrid